prototype(CodeQ.LinkChecker:Component.Links.List) < prototype(Neos.Fusion:Component) {
    links = ${[{}]}

    renderer = afx`
        <table data-sort-table class="neos-table codeq-link-checker-table">
            <thead>
                <tr>
                    <th data-sort-column>{I18n.translate('CodeQ.LinkChecker:Modules:domain')}</th>
                    <th data-sort-column>{I18n.translate('CodeQ.LinkChecker:Modules:source')}</th>
                    <th>{I18n.translate('CodeQ.LinkChecker:Modules:targetUriPath')}</th>
                    <th>{I18n.translate('CodeQ.LinkChecker:Modules:error')}</th>
                    <th>{I18n.translate('CodeQ.LinkChecker:Modules:foundAt')}</th>
                    <th></th>
                </tr>
            </thead>
            <tbody>
            <Neos.Fusion:Loop items={props.links} itemName="link">
                <CodeQ.LinkChecker:Component.Links.List.Link link={link}/>
            </Neos.Fusion:Loop>
            </tbody>
        </table>
        <script>{StaticResource.content("CodeQ.LinkChecker", "Private/FusionModule/Components/Links.List.js")}</script>
    `
}

prototype(CodeQ.LinkChecker:Component.Links.List.Link) < prototype(Neos.Fusion:Component) {
    links = ${{}}

    sourcePath = Neos.Fusion:Case {
        isNode {
            condition = ${String.startsWith(link.sourcePath, '/sites')}

            // its hacky that we dont use a proper breadcrumb rendering here,
            // but for that we need best need to go full php in terms of integrations
            renderer = Sitegeist.FusionLinkPrototypes:ActionUri {
                package = 'Neos.Neos'
                controller = 'Frontend\\Node'
                action = 'show'
                arguments.node = ${link.sourcePath}

                @process.removeLeadingSlash = ${String.trim(value, '/')}
                @process.replaceSlashWithArrow = ${String.replace(value, '/', ' > ')}
            }
        }

        isExternal {
            condition = ${String.startsWith(link.sourcePath, 'http')}
            // thanks to: https://stackoverflow.com/a/441771
            renderer = ${String.pregReplace(link.sourcePath, '#(https?://[^/]*)(/.*)#', '$2')}
        }

        fallback {
            condition = ${true}
            renderer = '#'
        }
    }

    sourceLinkToEditNode = Sitegeist.FusionLinkPrototypes:ActionUri {
        package = 'Neos.Neos.Ui'
        controller = 'Backend'
        action = 'index'
        arguments.node = ${link.sourcePath + '@' + Neos.Ui.Workspace.getPersonalWorkspace()['name']}
    }

    frontendUrl = Neos.Fusion:Case {
        isNode {
            condition = ${String.startsWith(link.sourcePath, '/sites')}

            renderer = Sitegeist.FusionLinkPrototypes:ActionUri {
                package = 'Neos.Neos'
                controller = 'Frontend\\Node'
                action = 'show'
                arguments.node = ${link.sourcePath}
            }
        }

        isExternal {
            condition = ${String.startsWith(link.sourcePath, 'http')}

            renderer = ${link.sourcePath}
        }

        fallback {
            condition = ${true}
            renderer = '#'
        }
    }

    targetLinkToEditNodeOrExternalLink = Neos.Fusion:Case {
        isNode {
            condition = ${String.startsWith(link.target, 'node://')}

            renderer = Sitegeist.FusionLinkPrototypes:ActionUri {
                package = 'Neos.Neos.Ui'
                controller = 'Backend'
                action = 'index'
                arguments.node = ${link.targetPath + '@' + Neos.Ui.Workspace.getPersonalWorkspace()['name']}
            }
        }

        isExternal {
            condition = ${String.startsWith(link.target, 'http')}

            renderer = ${link.target}
        }
    }

    deleteUri = Neos.Fusion:UriBuilder {
        action = 'delete'
        arguments.resultItem = ${link}
    }
    ignoreUri = Neos.Fusion:UriBuilder {
        action = 'ignore'
        arguments.resultItem = ${link}
    }

    renderer = afx`
        <tr>
            <td>{link.domain}</td>
            <td>
                {props.sourcePath}
                <a @if={String.startsWith(link.sourcePath, '/sites')} href={props.sourceLinkToEditNode} target="_blank" class="neos-button neos-button--small">
                    <i class="fas fa-edit"></i>
                </a>
                <a href={props.frontendUrl} target="_blank" class="neos-button neos-button--small">
                    <i class="fas fa-external-link-alt"></i>
                </a>
            </td>
            <td>
                <Neos.Fusion:Fragment @if.has={props.targetLinkToEditNodeOrExternalLink}>
                    <span class="target-page-text">{link.targetPageTitle || link.target}</span>
                    <a href={props.targetLinkToEditNodeOrExternalLink} target="_blank" class="neos-button neos-button--small">
                        <i class="fas fa-external-link-alt"></i>
                    </a>
                </Neos.Fusion:Fragment>
                <span @if.not={!props.targetLinkToEditNodeOrExternalLink}>{link.target}</span>
            </td>
            <td>{I18n.translate('CodeQ.LinkChecker:Modules:error.' + link.statusCode)}</td>
            <td>{Date.format(link.checkedAt, 'd.m.Y H:i')}</td>
            <td class="neos-action">
                <div class="neos-pull-right">
                    <Neos.Fusion.Form:Form form.target={props.deleteUri} attributes.class="neos-inline">
                        <Neos.Fusion.Form:Button
                            attributes.title={I18n.translate('CodeQ.LinkChecker:Modules:list.action.delete')}
                        >
                            <i class="fas fa-check"></i>
                        </Neos.Fusion.Form:Button>
                    </Neos.Fusion.Form:Form>

                    <Neos.Fusion.Form:Form form.target={props.ignoreUri} attributes.class="neos-inline">
                        <Neos.Fusion.Form:Button
                            attributes.title={I18n.translate('CodeQ.LinkChecker:Modules:list.action.ignore')}
                        >
                            <i class="fas fa-eye-slash"></i>
                        </Neos.Fusion.Form:Button>
                    </Neos.Fusion.Form:Form>
                </div>
            </td>
        </tr>
    `
}
